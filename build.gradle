plugins {
    id "idea"
    id "maven-publish"
    id "eclipse"
    id 'com.diffplug.spotless' version '7.0.2'
    id 'net.neoforged.moddev.legacyforge' version '2.0.91'
    id 'java-library'
}

sourceSets {
    main.resources {
        srcDir 'src/generated/resources'
    }
}

mixin {
    add sourceSets.main, 'mixins.monilabs.refmap.json'
    config 'monilabs.mixins.json'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'GTCEu Maven'
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
        }
    }
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        name 'FirstDarkDev'
        url "https://maven.firstdarkdev.xyz/snapshots/"
    }
    maven {
        // latvian.dev Maven (KubeJS and Rhino)
        url = "https://maven.latvian.dev/releases"
    }
    maven { // Registrate
        url = "https://maven.tterrag.com/"
        content {
            // need to be specific here due to version overlaps
            includeGroup("com.tterrag.registrate")
        }
    }
    maven {
        // Patchouli, JEI
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        // Curse Forge File
        url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // EMI
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven {
        url = "https://maven.architectury.dev"
    }
    maven {
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        // For Gradle 5.1 and above, limit it to just AE2
        content {
            includeGroup 'appeng'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "ftbMavenReleases"
        url = uri("https://maven.ftb.dev/releases")
    }
}

version = mod_version
group = maven_group
base { archivesName = archives_base_name }
java { sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17 }
sourceSets.main.resources { srcDir 'src/generated/resources' }

legacyForge {
    version = project.minecraft_version + '-' + project.forge_version

    parchment {
        mappingsVersion = project.mapping_version
        minecraftVersion = project.minecraft_version
    }

    runs {
        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.INFO
        }
        client {
            client()
            sourceSet = sourceSets.main
            programArguments.addAll('--refresh-dependencies')
            systemProperty('forge.enabledGameTestNamespaces', project.mod_id)
        }
        server {
            server()
            sourceSet = sourceSets.main
            systemProperty('forge.enabledGameTestNamespaces', project.mod_id)
            programArguments.addAll('--nogui', '--world', 'world-extra')
        }
        data {
            data()
            sourceSet = sourceSets.main

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll('--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath())
            programArguments.addAll('--existing-mod', 'gtceu')
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

jar {
    manifest.attributes([
            "MixinConfigs": "monilabs.mixins.json"
    ])
}

apply from: "$rootDir/gradle/scripts/spotless.gradle"

dependencies {
    compileOnly("org.jetbrains:annotations:26.0.1")

    // Apply Mixin AP
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor')

    // JEI, EMI, Jade
    modCompileOnly("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    modCompileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    modImplementation("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    modImplementation("dev.emi:emi-forge:${emi_version}+${minecraft_version}")
    modImplementation("maven.modrinth:jade:${jade_version}")

    modImplementation("dev.ftb.mods:ftb-library-forge:${ftblibrary_version}")
    modImplementation("dev.ftb.mods:ftb-teams-forge:${ftbteams_version}")

    // GregTech and dependencies
    modImplementation("com.gregtechceu.gtceu:gtceu-${minecraft_version}:${gtceu_version}:slim") { transitive = false }
    modImplementation("com.lowdragmc.ldlib:ldlib-forge-${minecraft_version}:${ldlib_version}") { transitive = false }
    modImplementation("com.tterrag.registrate:Registrate:${registrate_version}")
    modImplementation("dev.latvian.mods:kubejs-forge:${kubejs_version}")
    modImplementation("dev.latvian.mods:rhino-forge:${rhino_version}")
    modImplementation("dev.toma.configuration:configuration-forge-${minecraft_version}:${configuration_version}")
    modImplementation("dev.architectury:architectury-forge:${architectury_version}")
    modImplementation("appeng:appliedenergistics2-forge:${appeng_version}")
    modImplementation("org.appliedenergistics:guideme:${guideme_version}")

    modImplementation("maven.modrinth:oculus:${oculus_version}")
    modImplementation("maven.modrinth:embeddium:${embeddium_version}")

    // FancyMenu
    // Uses curse maven for now idk if there is another way
    modImplementation("curse.maven:fancymenu-367706:6918987")
    modImplementation("curse.maven:konkrete-410295:5028413")
    modImplementation("curse.maven:melody-938643:5109692")

    // lombok
    compileOnly('org.projectlombok:lombok:1.18.36')
    annotationProcessor('org.projectlombok:lombok:1.18.36')
}

tasks.named('processResources', ProcessResources).configure {
    var properties = [
            "mod_license": mod_license,
            "mod_id": mod_id,
            "version": version,
            "mod_name": mod_name,
            "mod_url": mod_url,
            "mod_author": mod_author,
            "forge_version": forge_version.split("\\.")[0], // only specify major version of forge
            "minecraft_version": minecraft_version,
            "gtceu_version": gtceu_version,
    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties + [project: project]
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.named('jar', Jar).configure { finalizedBy 'reobfJar' }
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    options.encoding = "UTF-8"
    options.release.set(17)
}